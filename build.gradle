import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.10.2'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.4.21"
    id 'jacoco'
}

repositories {
    jcenter()
    mavenCentral()

    maven { url "http://maven.sk89q.com/repo/" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
}

group = 'io.github.poeschl.bukkit'

processResources {
    filter ReplaceTokens, tokens: [
            "name"   : project.name,
            "version": project.version
    ]
}

jar {
    manifest {
        attributes("Project": project.name,
                "Version": project.version)
    }
}

shadowJar {
    configurations = [project.configurations.compile]
    classifier = null
}

coveralls {
    sourceDirs += ['src/main/kotlin']
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: ["**/InstanceFactory.*"])
            }))
        }
    }
}

release {
    preTagCommitMessage = '[Gradle Release Plugin] - Version: '
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:$bukkit_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "org.spigotmc:spigot-api:$bukkit_version"
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:3.7.7'
    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompile 'com.nhaarman:mockito-kotlin:0.12.2'


}
